plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
}
apply from: "${rootProject.rootDir}/eclipse_swt.gradle"
apply from: "${rootProject.rootDir}/url_file.gradle"

group 'de.willuhn.jameica'
version '2.8-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'ISO-8859-1'
}
tasks.withType(Javadoc) {
    options.encoding = 'ISO-8859-1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib:util')
    implementation project(':lib:datasource')

    implementation group: 'be.cyberelf.nanoxml', name: 'nanoxml', version: '2.2.3' // (last update: 2008-06-16)
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.56'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.56'
    implementation group: 'org.eclipse.platform', name: 'org.eclipse.jface', version: '3.15.100'
    implementation group: 'org.eclipse.platform', name: 'org.eclipse.ui.forms', version: '3.7.500'
    // Eclipse SWT ist plattformabhängig
    implementation group: 'org.eclipse.platform', name: "org.eclipse.swt.${swtPlatform}", version: swtVersion
    // Java EE. Ab Java 11 nicht mehr in Java enthalten
    // Java SE 9: JAXB 2.3.1
    // Java SE 8: JAXB 2.2.8
    implementation 'javax.xml.ws:jaxws-api:2.2.8'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.2.11' // JAVA 9 => '2.4.0-b180830.0438'
    implementation 'org.glassfish.main.javaee-api:javax.jws:3.1.2.2'

    // Printing plug-in for SWT. See <https://code.google.com/archive/p/swt-paperclips/> (last update: 2009-08-12)
    implementation urlFile('paperclips-1.0.4', 'https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/swt-paperclips/net.sf.paperclips_1.0.4.200908120926.jar')

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// solange noch die Logik des ANT-Builds benötigt wird, nutze einen Unterordner
buildDir = file("./build/gradle/")
jar {
    baseName "de_willuhn_${project.name}"
    manifest {
        attributes "Main-Class": "de.willuhn.jameica.Main"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = false
    }
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}
