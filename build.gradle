plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
}
apply from: "${rootProject.rootDir}/gradle/url_file.gradle"

group 'de.willuhn.jameica'
version '2.8-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'ISO-8859-1'
}
tasks.withType(Javadoc) {
    options.encoding = 'ISO-8859-1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib:util')
    implementation project(':lib:datasource')

    implementation libs.bundles.bouncycastle
    implementation libs.bundles.javaee
    implementation libs.apache.commons.lang
    implementation libs.apache.commons.cli
    implementation libs.apache.velocity
    implementation libs.javax.annotation
    implementation libs.eclipse.jface
    implementation libs.eclipse.ui.forms
    implementation libs.nanoxml
    implementation swt.platform

    // Printing plug-in for SWT. See <https://code.google.com/archive/p/swt-paperclips/> (last update: 2009-08-12)
    implementation urlFile('paperclips-1.0.4', 'https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/swt-paperclips/net.sf.paperclips_1.0.4.200908120926.jar')

    testImplementation libs.junit
}

// solange noch die Logik des ANT-Builds ben√∂tigt wird, nutze einen Unterordner
buildDir = file("./build/gradle/")
jar {
    baseName "de_willuhn_${project.name}"
    manifest {
        attributes "Main-Class": "de.willuhn.jameica.Main"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = false
    }
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}
